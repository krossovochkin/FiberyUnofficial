apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.google.dagger.hilt.android'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply from: '../quality/lint.gradle'

android {
    compileSdk libs.versions.android.compile.sdk.get().toInteger()
    testBuildType "debug"

    defaultConfig {
        applicationId "com.krossovochkin.fiberyunofficial"
        minSdk libs.versions.android.min.sdk.get().toInteger()
        targetSdk libs.versions.android.target.sdk.get().toInteger()
        versionCode libs.versions.android.version.code.get().toInteger()
        versionName libs.versions.android.version.name.get()
        testInstrumentationRunner "com.krossovochkin.fiberyunofficial.FiberyTestRunner"
    }

    signingConfigs {
        release {
            storeFile file("$rootDir/release-keystore-fibery-unofficial.jks")
            storePassword System.getenv()['RELEASE_STORE_PASSWORD']
            keyAlias System.getenv()['RELEASE_KEY_ALIAS']
            keyPassword System.getenv()['RELEASE_KEY_PASSWORD']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlin {
        jvmToolchain(21)
    }

    buildFeatures {
        viewBinding = true
    }

    packagingOptions {
        exclude 'META-INF/versions/9/previous-compilation-data.bin'
    }

    /* TODO: enable tests
    testOptions {
        animationsDisabled = true

        managedDevices {
            allDevices {
                pixel2api30 (com.android.build.api.dsl.ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 30
                    systemImageSource = "aosp-atd"
                }
            }
        }
    }
     */

    namespace 'com.krossovochkin.fiberyunofficial'
}

dependencies {
    implementation project(":core:data:api")
    implementation project(":core:data:auth")
    implementation project(":core:data:serialization")
    implementation project(":core:domain")
    implementation project(":core:presentation:viewbinding")
    implementation project(":core:presentation:result")

    implementation project(":feature:login")
    implementation project(":feature:app-list")
    implementation project(":feature:entity-type-list")
    implementation project(":feature:entity-list")
    implementation project(":feature:entity-details")
    implementation project(":feature:entity-create")
    implementation project(":feature:entity-create-domain")
    implementation project(":feature:picker-entity")
    implementation project(":feature:picker-single-select")
    implementation project(":feature:picker-multi-select")
    implementation project(":feature:picker-filter")
    implementation project(":feature:picker-sort")
    implementation project(":feature:file-list")
    implementation project(":feature:comment-list")

    implementation libs.dagger
    ksp libs.dagger.compiler
    implementation libs.converter.moshi

    implementation libs.appcompat
    implementation libs.constraintlayout
    implementation libs.fragment.ktx
    implementation libs.navigation.fragment.ktx
    implementation libs.security.crypto
    implementation libs.core.splashscreen

    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.ext.junit
	androidTestImplementation libs.okhttp3
    kspAndroidTest libs.dagger.compiler
}
