apply plugin: "com.github.ben-manes.versions"
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "kover"
apply from: "$rootDir/quality/deps_lint.gradle"

buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath d.plugin.gradle_android
        classpath d.plugin.gradle_kotlin
        classpath d.plugin.gradle_navigation
        classpath d.plugin.gradle_versions
        classpath d.plugin.gradle_detekt
        classpath d.plugin.gradle_dependencies_lint
        classpath d.plugin.gradle_kover
    }
}

dependencies {
    detektPlugins d.plugin.gradle_detekt_formatting
}
//noinspection UnnecessaryQualifiedReference
task detektAll(type: io.gitlab.arturbosch.detekt.Detekt) {
    allRules = true
    parallel = true
    setSource(files(projectDir))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
    buildUponDefaultConfig = true
    config.from(files("$rootDir/quality/detekt.yml"))
    reports {
        html.enabled = false
        xml.enabled = false
        txt.enabled = false
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
